# Description:
#   Tensor Standard Libraries.
#
#   The libraries in this package are not allowed to have ANY dependencies
#   to other TF components outside of TSL.

load(
    "//tsl/platform:rules_cc.bzl",
    "cc_library",
)
load(
    "//tsl:tsl.bzl",
    "check_deps",
    "tf_copts",
)
load("//third_party/tensorflow:tensorflow.default.bzl", "filegroup", "get_compatible_with_portable")
load(
    "//tsl/platform:build_config_root.bzl",
    "if_static",
)

package(
    default_visibility = [
        "//visibility:public",
    ],
    licenses = ["notice"],
)

filegroup(
    name = "mobile_srcs_only_runtime",
    srcs = [
        "command_line_flags.cc",
        "command_line_flags.h",
        "determinism.cc",
        "determinism.h",
        "env_var.cc",
        "env_var.h",
        "ptr_util.h",
        "use_cudnn.cc",
        "use_cudnn.h",
    ],
)

filegroup(
    name = "determnism_hdr",
    srcs = [
        "determinism.h",
    ],
    compatible_with = get_compatible_with_portable(),
    visibility = [
        "//third_party/tensorflow:__subpackages__",
        "//third_party/tensorflow/core/util:__pkg__",
    ],
)

filegroup(
    name = "framework_internal_private_hdrs",
    srcs = [
        "command_line_flags.h",
        "env_var.h",
        "ptr_util.h",
        "use_cudnn.h",
    ],
)

filegroup(
    name = "framework_internal_impl_srcs",
    srcs = [
        "use_cudnn.cc",
    ],
)

filegroup(
    name = "lib_internal_public_hdrs",
    srcs = [
        "command_line_flags.h",
        "env_var.h",
        "use_cudnn.h",
    ],
    visibility = [
        "//third_party/tensorflow/core:__pkg__",
        "//third_party/tensorflow/core/util:__pkg__",
    ],
)

filegroup(
    name = "determinism_hdr",
    srcs = [
        "determinism.h",
    ],
    compatible_with = get_compatible_with_portable(),
    visibility = [
        "//third_party/tensorflow:__subpackages__",
        "//third_party/tensorflow/core/util:__pkg__",
    ],
)

filegroup(
    name = "framework_srcs",
    srcs = [
        "ptr_util.h",
        "use_cudnn.h",
    ],
)

cc_library(
    name = "determinism_hdr_lib",
    hdrs = [":determinism_hdr"],
    compatible_with = get_compatible_with_portable(),
    visibility = ["//visibility:private"],
)

# Note: This rule should not be used as a dependency for kernels. Use the
# "determinism_for_kernels" rule below instead.
cc_library(
    name = "determinism",
    srcs = ["determinism.cc"],
    hdrs = ["determinism.h"],
    copts = tf_copts(),
    visibility = ["//third_party/tensorflow:__subpackages__"],
    deps = [
        ":env_var",
        "//tsl/platform:mutex",
        "@com_google_absl//absl/strings",
    ],
    alwayslink = 1,
)

# This alias should be used as a dependency for kernels which use determinism,
# as well any other rules which are in the same shared library as the kernels.
# This rule does not include the determinism.cc file for nonstatic builds. The
# reason is that for nonstatic builds, the shared object which contains the
# kernels (e.g. _pywrap_tensorflow_internal.so) must not contain the global
# variable in determinism.cc, since the global variable is already in
# libtensorflow_framework.so.
#
# To test that determinism.cc is not improperly included in the shared object
# which contains the kernels, you can run the "determinism_check_deps" rule
# below.
alias(
    name = "determinism_for_kernels",
    actual = if_static(":determinism", ":determinism_hdr_lib"),
    visibility = ["//third_party/tensorflow:__subpackages__"],
)

check_deps(
    name = "determinism_check_deps",
    disallowed_deps = if_static(
        [],
        otherwise = [":determinism"],
    ),
    deps = [
    ],
)

cc_library(
    name = "determinism_test_util",
    hdrs = [":determinism_test_util.h"],
    data = [
        # Adding this data dependency ensures determinism_check_deps is run
        # whenever determinism tests are run.
        ":determinism_check_deps",
    ],
    deps = [":determinism"],
)

cc_library(
    name = "env_var",
    srcs = ["env_var.cc"],
    hdrs = ["env_var.h"],
    deps = [
        "//tsl/platform:errors",
        "//tsl/platform:logging",
        "//tsl/platform:numbers",
        "//tsl/platform:status",
        "//tsl/platform:str_util",
        "//tsl/platform:strcat",
        "//tsl/platform:stringpiece",
        "//tsl/platform:types",
    ],
)

cc_library(
    name = "ptr_util",
    hdrs = ["ptr_util.h"],
)

cc_library(
    name = "reporter",
    srcs = ["reporter.cc"],
    hdrs = ["reporter.h"],
    visibility = [
        "//third_party/tensorflow/core:__subpackages__",
        "//tsl:__subpackages__",
    ],
    deps = [
        "//third_party/tensorflow/core/util:test_log_proto_impl_cc",
        "//tsl/platform:env",
        "//tsl/platform:env_impl",
        "//tsl/platform:errors",
        "//tsl/platform:macros",
        "//tsl/platform:mutex",
        "//tsl/platform:str_util",
        "//tsl/platform:types",
    ],
)

cc_library(
    name = "command_line_flags",
    srcs = ["command_line_flags.cc"],
    hdrs = ["command_line_flags.h"],
    deps = [
        "//tsl/platform:logging",
        "//tsl/platform:str_util",
        "//tsl/platform:stringpiece",
        "//tsl/platform:stringprintf",
        "//tsl/platform:types",
    ],
)

filegroup(
    name = "test_hdrs",
    testonly = 1,
    srcs = [
        "reporter.h",
    ],
    visibility = ["//third_party/tensorflow/core/util:__pkg__"],
)

filegroup(
    name = "android_test_hdrs",
    testonly = 1,
    srcs = [
        "reporter.h",
    ],
    visibility = [
        "//third_party/tensorflow/core:__pkg__",
        "//third_party/tensorflow/core/util:__pkg__",
    ],
)

filegroup(
    name = "android_test_srcs",
    testonly = 1,
    srcs = [
        "reporter.cc",
        ":android_test_hdrs",
    ],
    visibility = [
        "//third_party/tensorflow/core:__pkg__",
        "//third_party/tensorflow/core/util:__pkg__",
    ],
)
